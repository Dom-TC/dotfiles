#!/bin/zsh

# This script is used by Hazel to sort DCS receipts into the correct folders.
# A file is stored in the `_Incoming` folder with one of two name formats:
# - `SID_Receipt_YYYY_MM_DD_HH_MM_SS`
# - `SID_Personal_<INITIALS>_YYYY_MM_DD_HH_MM_SS` Where <INITIALS> are the two letter initials of the user who made the purchase
# If the file matches a, it's moved to the correct month folder
# If it matches b, it's moved into a subfolder named `Personal Receipts` within the correct month folder.


# Define base directory for receipts
if [[ -d "$HOME/Dropbox" ]]; then
  BASE_DIR="$HOME/Dropbox/2 Areas/Snailed It Development Ltd/Receipts/Next Quarter"
else
  echo "Dropbox folder not found!"
  exit 1
fi

INCOMING_DIR="${BASE_DIR}/_Incoming"

# Ensure the file argument is provided
if [[ -z "$1" ]]; then
  echo "No file provided."
  exit 1
fi

FILE_PATH="$1"
FILE_NAME="$(basename "$FILE_PATH")"

# Check if the file exists
if [[ ! -f "$FILE_PATH" ]]; then
  echo "File does not exist: $FILE_PATH"
  exit 1
fi

# Match patterns
if [[ "$FILE_NAME" =~ ^SID_Receipt_([0-9]{4})-([0-9]{2})- ]]; then
  YEAR="${match[1]}"
  MONTH="${match[2]}"
  # Convert numeric month to name
  MONTH_NAME="$(date -jf "%m" "$MONTH" "+%B" 2>/dev/null || date -d "2024-$MONTH-01" "+%B")"
  TARGET_DIR="${BASE_DIR}/${MONTH} ${MONTH_NAME}/Company Account"
elif [[ "$FILE_NAME" =~ ^SID_Personal_([A-Z]{2})_([0-9]{4})-([0-9]{2})- ]]; then
  INITIALS="${match[1]}"
  YEAR="${match[2]}"
  MONTH="${match[3]}"
  # Convert numeric month to name
  MONTH_NAME="$(date -jf "%m" "$MONTH" "+%B" 2>/dev/null || date -d "2024-$MONTH-01" "+%B")"
  TARGET_DIR="${BASE_DIR}/${MONTH} ${MONTH_NAME}/Personal Account/${INITIALS}"
else
  echo "Filename does not match expected patterns: $FILE_NAME"
  exit 1
fi

# Create target directory if it doesn't exist
mkdir -p "$TARGET_DIR"

# Move the file
mv "$FILE_PATH" "$TARGET_DIR"

echo "Moved '$FILE_NAME' to '$TARGET_DIR'"
exit 0
